import de.undercouch.gradle.tasks.download.Download

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: "java"
apply plugin: "distribution"
apply plugin: "idea"

group "org.logstash.filters"
version "${new File("VERSION").text.trim()}"
project.archivesBaseName = "logstash-filter-geoip"

String junitVersion = '5.9.2'
String maxmindGeoip2Version = '4.2.0'
String maxmindDbVersion = '3.1.0'
String log4jVersion = '2.17.1'
String jrubyCompleteVersion = '9.1.13.0'
String mockitoVersion = '5.9.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: 'org.jruby', name: 'jruby-complete', version: "1.7.26"
    classpath 'de.undercouch:gradle-download-task:3.2.0'
  }
}

repositories {
  mavenCentral()
}

configurations {
  geolite2
}

dependencies {
  compileOnly group: "org.apache.logging.log4j", name: "log4j-api", version: log4jVersion
  compileOnly group: "org.apache.logging.log4j", name: "log4j-core", version: log4jVersion
  compileOnly group: 'org.jruby', name: 'jruby-complete', version: jrubyCompleteVersion
  compileOnly fileTree(dir: logstashCoreGemPath, include: '**/*.jar')

  implementation group: "com.maxmind.geoip2", name: "geoip2", version: maxmindGeoip2Version
  implementation group: "com.maxmind.db", name: "maxmind-db", version: maxmindDbVersion

  testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
  testImplementation group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
  testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
  testImplementation group: "org.apache.logging.log4j", name: "log4j-api", version: log4jVersion
  testImplementation group: 'org.jruby', name: 'jruby-complete', version: jrubyCompleteVersion
  testImplementation group: "org.mockito", name: "mockito-core", version: mockitoVersion

  geolite2('org.elasticsearch:geolite2-databases:20191119') {
    transitive = false
  }
}

test {
  useJUnitPlatform()

  testLogging {
    events "started", "passed", "skipped", "failed", "standard_out", "standard_error"
  }
}

configurations {
  testImplementation.extendsFrom compileOnly
}

task generateGemJarRequiresFile {
  doLast {
    File jars_file = file("lib/${project.archivesBaseName}_jars.rb")
    jars_file.newWriter().withWriter { w ->
      w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
      w << "require \'jar_dependencies\'\n"
      configurations.runtimeClasspath.allDependencies.each {
        w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
      }
      w << "require_jar(\'${project.group}\', \'${project.archivesBaseName}\', \'${project.version}\')\n"
    }
  }
}

task vendor {
  doLast {
    String vendorPathPrefix = "vendor/jar-dependencies"
    configurations.runtimeClasspath.allDependencies.each { dep ->
      File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
      String groupPath = dep.group.replaceAll('\\.', '/')
      File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
      newJarFile.mkdirs()
      Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
    }
    String projectGroupPath = project.group.replaceAll('\\.', '/')
    File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.archivesBaseName}/${project.version}/${project.archivesBaseName}-${project.version}.jar")
    projectJarFile.mkdirs()
    Files.copy(file("$buildDir/libs/${project.archivesBaseName}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
  }
}

// extract database from maven dependency jar
task unzip(type: Copy) {
  def outputDir = file("vendor")

  from(zipTree(configurations.geolite2.singleFile)) {
    include "GeoLite2-ASN.mmdb"
    include "GeoLite2-City.mmdb"
  }
  into outputDir
}

vendor.dependsOn(jar, generateGemJarRequiresFile, unzip)
